plugins {
    id 'java-library'
    id "groovy"
    id 'maven-publish'
}

group 'dev.inaka'
version '1.0.0'

dependencies {
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.apache.httpcomponents:httpmime:4.5.13'
    implementation 'commons-io:commons-io:2.11.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.12.16'
    testImplementation 'org.objenesis:objenesis:3.2'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.13'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
}

test {
    useJUnitPlatform()
}

task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveBaseName = "${rootProject.name}"
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            url = "https://maven.pkg.github.com/cherfia/jotenberg"
            credentials {
                username = "${github_actor}"
                password = "${github_token}"
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = 'dev.inaka'
            artifactId = 'jotenberg'
            version = '1.0.0'
            from components.java
            pom {
                name = 'Jotenberg'
                description = 'A Java library that interacts with Gotenberg\'s different modules to convert a variety of document formats to PDF files.'
                url = 'https://github.com/cherfia/jotenberg'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/cherfia/jotenberg/blob/main/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'cherfia'
                        name = 'Taha Abdelmoutaleb Cherfia'
                        email = 'taha.cherfia@gmail.com'
                    }
                }
            }
        }
    }

}

